-- A lock based upon the test-and-set operation with fixed-time backoff.
--  * Based on Herlihy & Shavit, Section 7.4. 
--  * @param delay the time in nanoseconds to delay when conflict detected.

include "variable.csp"
include "lock-spec.csp"

NTHREADS = 5

datatype ThreadID = T.{0..NTHREADS-1}
datatype LockID = L.{0}

channel delay: ThreadID


-- state variable
channel getState, setState : ThreadID . Bool
channel gASState : ThreadID . Bool . Bool
State = Var(false, getState, setState, gASState)
InternalChannels = {|getState, setState, gASState, delay|}


Lock :: (ThreadID) -> Proc
Lock(t) =  getState.t?s -> if s == True then Lock(t) -- while(state.get()){ }
           else gASState.t?v!True -> if v == False then Holding(t) -- if(! state.getAndSet(true)) done = true
           else delay!t -> Lock(t)

Unlock :: (ThreadID) -> Proc
Unlock(t) = setState.t!False -> NotHolding(t) -- def unlock = state.set(false)

TryLock :: (ThreadID) -> Proc
TryLock(t) = getState.  t?s -> if s == True then lockNotObtained.L.0.t -> NotHolding(t) else gASState.t?v!True -> if v == False then Holding(t) else lockNotObtained.L.0.t -> NotHolding(t)  --!state.get && !state.getAndSet(true)

Holding(t) = Unlock(t)

NotHolding(t) = callLock.L.0.t -> Lock(t)--(TryLock(t) |~| Lock(t))


AllThreads = ||| t : ThreadID @ NotHolding(t)
AllChannels = Union({{|delay|}, InternalChannels, LockEvents})
ActualSystem = (AllThreads [|InternalChannels|] State)
ActualSystemR = ActualSystem [[gASState.t.False.True <- lockObtained.L.0.t, setState.t.False <- lockUnlocked.L.0.t | t <- ThreadID]] \ InternalChannels

-- Obvious
assert ActualSystem :[divergence free]
assert ActualSystemR :[divergence free]
assert ActualSystem :[deadlock free]


-- Check that no two threads hold the lock at the same time
assert CheckMutualExclusion(L.0) [T= ActualSystemR \ diff(LockEvents, OnlyRootObtain(L.0, ThreadID))--(ActualSystem\(diff(AllChannels, {gASState.t.False.True, setState.t.False | t <- ThreadID})))

-- Check no divergence before the lock is first held
CheckNoDiv = gASState?t!False.True -> STOP
             [] getState?t._ -> CheckNoDiv
assert (ActualSystem [|{|gASState, getState|}|] CheckNoDiv) \ {|getState|} :[divergence free]


assert LockSpec(L.0, {}, ThreadID) [T= ActualSystemR
assert ActualSystemR [T= LockSpec(L.0, {}, ThreadID)